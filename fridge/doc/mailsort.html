<h1><join> Mail Sort</join></h1>
<h2>Contents</h2>
<div id="htmltoc">
<h2><font color=black>&bull;</font></a> <a href=#1>Author</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#2>Download</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#3>Description</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#4>Code</a></h2>
<h3><font color=black>&bull;</font></a> <a href=#5>Main</a></h3>
<h3><font color=black>&bull;</font></a> <a href=#6>compute_date</a></h3>
<h3><font color=black>&bull;</font></a> <a href=#7>days_in_month</a></h3>
<h3><font color=black>&bull;</font></a> <a href=#8>canonacalize_subject</a></h3>
<h2><font color=black>&bull;</font></a> <a href=#9>Copyright</a></h2>
</div><!--- htmltoc --->
<div id="htmlbody">
<a name=1></a><h2> Author</h2>
<P>
Arnold Robbins
<a name=2></a><h2> Download</h2>
<P>
Download from
<a href="http://lawker.googlecode.com/svn/fridge/lib/awk/mailsort.awk">LAWKER.</a>
<a name=3></a><h2> Description</h2>
<P>
Sorts a Unix style mailbox by "thread", in date+subject order.
<P>
This is a  script I use quite a lot.  It requires gawk although with some work could
be ported to standard awk.  The timezone offset from GMT has to be
adjust to one's local offset, although I could probably eliminate
that if I wanted to work on it hard enough.
<P>
This took me a while to write and get right, but it's been working
flawlessly for a few years now.
The script uses Message-ID header to detect and remove duplicates.  It requires GNU Awk for
 time/date functions and for efficiency hack in string concatenation but could
 be made to run on a POSIX awk with some work.
<a name=4></a><h2> Code</h2>
<a name=5></a><H3> Main</H3>
<SMALL>
<PRE>
BEGIN {
       TRUE = 1
       FALSE = 0

       split("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec", months, " ")
       for (i in months)
               Month[months[i]] = i    # map name to number

       MonthDays[1] = 31
       MonthDays[2] = 28       # not used
       MonthDays[3] = 31
       MonthDays[4] = 30
       MonthDays[5] = 31
       MonthDays[6] = 30
       MonthDays[7] = 31
       MonthDays[8] = 31
       MonthDays[9] = 30
       MonthDays[10] = 31
       MonthDays[11] = 30
       MonthDays[12] = 31

       In_header = FALSE
       Body = ""

       LocalOffset = 2 # We are two hours ahead of GMT

       # These keep --lint happier
       Debug = 0
       MessageNum = 0
       Duplicates = FALSE
}

/^From / {
       In_header = TRUE
       if (MessageNum)
               Text[MessageNum] = Body
       MessageNum++
       Body = ""
 # print MessageNum
}

In_header && /^Date: / {
       Date[MessageNum] = compute_date($0)
}

In_header && /^Subject: / {
       Subject[MessageNum] = canonacalize_subject($0)
}

In_header && /^Message-[Ii][Dd]: / {
       if (NF == 1) {
               getline junk
               $0 = $0 RT junk # Preserve original input text!
       }

       # Note: Do not use $0 directly; it's needed as the Body text
       # later on.

       line = tolower($0)
       split(line, linefields)

       message_id = linefields[2]
       Mesg_ID[MessageNum] = message_id        # needed for disambiguating message
       if (message_id in Message_IDs) {
               printf("Message %d is duplicate of %s (%s)\n",
                       MessageNum, Message_IDs[message_id],
                       message_id) > "/dev/stderr"
               Message_IDs[message_id] = (Message_IDs[message_id] ", " MessageNum)
               Duplicates++
       } else {
               Message_IDs[message_id] = MessageNum ""
       }
}


In_header && /^$/ {
       In_header = FALSE
       # map subject and date to index into text

       if (Debug && (Subject[MessageNum], Date[MessageNum], Mesg_ID[MessageNum]) in SubjectDateId) {
               printf(\
       ("Message %d: Subject &lt;%s> Date &lt;%s> Message-ID &lt;%s> already in" \
       " SubjectDateId (Message %d, s: &lt;%s>, d &lt;%s> i &lt;%s>)!\n"),
               MessageNum, Subject[MessageNum], Date[MessageNum], Mesg_ID[MessageNum],
               SubjectDateId[Subject[MessageNum], Date[MessageNum], Mesg_ID[MessageNum]],
               Subject[SubjectDateId[Subject[MessageNum], Date[MessageNum], Mesg_ID[MessageNum]]],
               Date[SubjectDateId[Subject[MessageNum], Date[MessageNum], Mesg_ID[MessageNum]]],
               Mesg_ID[SubjectDateId[Subject[MessageNum], Date[MessageNum], Mesg_ID[MessageNum]]]) \
                       > "/dev/stderr"
       }

       SubjectDateId[Subject[MessageNum], Date[MessageNum], Mesg_ID[MessageNum]] = MessageNum

       if (Debug) {
               printf("\tMessage Num = %d, length(SubjectDateId) = %d\n",
                       MessageNum, length(SubjectDateId)) > "/dev/stderr"
               if (MessageNum != length(SubjectDateId) && ! Printed1) {
                       Printed1++
                       printf("---> Message %d &lt;---\n", MessageNum) > "/dev/stderr"
               }
       }

       # build up mapping of subject to earliest date for that subject
       if (! (Subject[MessageNum] in FirstDates) ||
           FirstDates[Subject[MessageNum]] > Date[MessageNum])
               FirstDates[Subject[MessageNum]] = Date[MessageNum]
}

{
       Body = Body ($0 "\n")
}

END {
       Text[MessageNum] = Body # get last message

       if (Debug) {
               printf("length(SubjectDateId) = %d, length(Subject) = %d, length(Date) = %d\n",
                       length(SubjectDateId), length(Subject), length(Date))
               printf("length(FirstDates) = %d\n", length(FirstDates))
       }

       # Create new array to sort by thread. Subscript is
       # earliest date, subject, actual date
       for (i in SubjectDateId) {
               n = split(i, t, SUBSEP)
               if (n != 3) {
                       printf("yowsa! n != 3 (n == %d)\n", n) > "/dev/stderr"
                       exit 1
               }
               # now have subject, date, message-id in t
               # create index into Text
               Thread[FirstDates[t[1]], i] = SubjectDateId[i]
       }

       n = asorti(Thread, SortedThread)        # Shazzam!

       if (Debug) {
               printf("length(Thread) = %d, length(SortedThread) = %d\n",
                       length(Thread), length(SortedThread))
       }
       if (n != MessageNum && ! Duplicates) {
               printf("yowsa! n != MessageNum (n == %d, MessageNum == %d)\n",
                       n, MessageNum) > "/dev/stderr"
	#               exit 1
       }

       if (Debug) {
               for (i = 1; i &lt;= n; i++)
                       printf("SortedThread[%d] = %s, Thread[SortedThread[%d]] = %d\n",
                               i, SortedThread[i], i, Thread[SortedThread[i]]) > "DUMP1"
               close("DUMP1")
               if (Debug ~ /exit/)
                       exit 0
       }

       for (i = 1; i &lt;= MessageNum; i++) {
               if (Debug) {
                       printf("Date[%d] = %s\n",
                               i, strftime("%c", Date[i]))
                       printf("Subject[%d] = %s\n", i, Subject[i])
               }

               printf("%s", Text[Thread[SortedThread[i]]]) > "OUTPUT"
       }
       close("OUTPUT")

       close("/dev/stderr")    # shuts up --lint
}
</PRE>
</SMALL>
<a name=6></a><H3> compute_date</H3>
<P>
 Pull apart a date string and convert to timestamp.
<SMALL>
<PRE>
function compute_date(date_rec,         fields, year, month, day,
                                       hour, min, sec, tzoff, timestamp)
{
       split(date_rec, fields, "[:, ]+")
       if ($2 ~ /Sun|Mon|Tue|Wed|Thu|Fri|Sat/) {
               # Date: Thu, 05 Jan 2006 17:11:26 -0500
               year = fields[5]
               month = Month[fields[4]]
               day = fields[3] + 0
               hour = fields[6]
               min = fields[7]
               sec = fields[8]
               tzoff = fields[9] + 0
       } else {
               # Date: 05 Jan 2006 17:11:26 -0500
               year = fields[4]
               month = Month[fields[3]]
               day = fields[2] + 0
               hour = fields[5]
               min = fields[6]
               sec = fields[7]
               tzoff = fields[8] + 0
       }
       if (tzoff == "GMT" || tzoff == "gmt")
               tzoff = 0
       tzoff /= 100    # assume offsets are in whole hours
       tzoff = -tzoff

       # crude compensation for timezone
       # mktime() wants a local time:
       #       hour + tzoff yields GMT
       #       GMT + LocalOffset yields local time
       hour += tzoff + LocalOffset

       # if moved into next day, reset other values
       if (hour > 23) {
               hour %= 24
               day++
               if (day > days_in_month(month, year)) {
                       day = 1
                       month++
                       if (month > 12) {
                               month = 1
                               year++
                       }
               }
       }

       timestamp = mktime(sprintf("%d %d %d %d %d %d -1",
                               year, month, day, hour, min, sec))

       # timestamps can be 9 or 10 digits.
       # canonicalize them into 11 digits with leading zeros
       return sprintf("%011d", timestamp)
}
</PRE>
</SMALL>
<a name=7></a><H3> days_in_month</H3>
<P>
 How many days in the given month?
<SMALL>
<PRE>
function days_in_month(month, year)
{
       if (month != 2)
               return MonthDays[month]

       if (year % 4 == 0 && year % 400 != 0)
               return 29

       return 28
}
</PRE>
</SMALL>
<a name=8></a><H3> canonacalize_subject</H3>
<P>
Trim out "Re:", white space.
<SMALL>
<PRE>
function canonacalize_subject(subj_line)
{
       subj_line = tolower(subj_line)
       sub(/^subject: +/, "", subj_line)
       sub(/^(re: *)+/, "", subj_line)
       sub(/[[:space:]]+$/, "", subj_line)
       gsub(/[[:space:]]+/, " ", subj_line)

       return subj_line
}
</PRE>
</SMALL>
<a name=9></a><H2> Copyright</H2>
<P>
 Copyright 2007, 2008, Arnold David Robbins
 arnold@skeeve.com
</div><!--- htmlbody --->
