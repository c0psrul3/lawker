<h1><join> Simpler Handler of Command-Line Options</join></h1>
<h2>Contents</h2>
<div id="htmltoc">
<h2><font color=black>&bull;</font></a> <a href=#1>Synposis</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#2>Download</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#3>Description</a></h2>
<h3><font color=black>&bull;</font></a> <a href=#4>Define Switches and Defaults</a></h3>
<h2><font color=black>&bull;</font></a> <a href=#5>The Options function</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#6>Example</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#7>Code</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#8>Author</a></h2>
</div><!--- htmltoc --->
<div id="htmlbody">
!/sw/bin/gawk -f 

<a name=1></a><H2> Synposis</H2>
<PRE>
 optionsDemo [-h][-c][-a][-P who] 
</PRE>
<a name=2></a><H2> Download</H2>
<P>
<a href="http://lawker.googlecode.com/svn/fridge/lib/awk/optionsDemo.awk">LAWKER.</a>
<a name=3></a><H2> Description</H2>
<P>
I've never been happy with standard methods for passing variables into Awk. Command lines like:
<PRE>
gawk -f mycode.awk -v Seed=$RANDOM -v OutputDir=$HOME/tmp -v MaxErrors=2
</PRE>
leave the code defenceless agains one-letter typos in the variable names.
<P>
Similary, seperate config files like:
<PRE>
# file= myconfigrc.awk
BEGIN {Seed=ENVIRON["RANDOM"]
       OutputDir=ENVIRON["HOME"] "/tmp"
       MaxErrors=2
}
</PRE>
allow for command lines like this:
<PRE>
gawk -f mycode.awk -f configrc.awk
</PRE>
but if another script wants to call <tt>mycode</tt> many different
ways then the other script has to do sone, possible tedious, mucking around with temporary
files. Also, the <em>myconfigrc.awk</em> option is actually as bad, or worse, than
the commmand-line approach:
<UL>
<LI>
 As before, it leaves the code defenceless against one-letter
typos. 
<LI>
Further, it offers less functionality that the command-line option. For example,
the within Awk access to <em>RANDOM</em> seen at top of <em>myconfigrc</em> returns
an empty string, and not a system-generated random value.
</UL>
<P>
I've often been tempted by the brevity of the "within-string" option:
<PRE>
gawk -f mycode.awk --source '
      BEGIN {Seed='$RANDOM'; OutputDir='$HOME/tmp'; MaxErrors='2';}'
</PRE>
but this approach is a little too arcane for most folks, which makes it error
prone to modify.
<P>
Because of the above, I am a frequent user of Arnold Robbin's excellent
"getopt" code that
allows command lines like
<PRE>
gawk -f mycode.awk -s $RANDOM -o $HOME/tmp -e 2
</PRE>
but even this code has drawbacks:
<UL>
<LI> It does not allow for "long names" such as "--help";</LI>
<LI>
It introduces five global variables into Awk. Lately I've been building lots
of library code in Awk and I have become obsessive about <em>not</em> polluting
the global name space with new variables. 
</UL>
<P>
Also, Arnold's "getopt" is  perhaps too complex. 
For example, in the following, I show 13 lines of code
that replaces the 80 lines of "getopt", 
handles the common usages of Arnold's function,
uses no global variables, and supports long names.
</UL>
<P>
(Embarressed cough: I do actually use one global variable in the following. Lately
I've come to view globals 
like chemotherapy- not desirable, but sometimes necessary.)
<a name=4></a><H3> Define Switches and Defaults</H3>
<P>
The code assumes that switches beginning with upper case letters 
take arguments, while lower case switches are booleans.
For example, the following string could be used to define
four switches, the first of which takes an argument (and has the default
value "world". 
<PRE>
"P = world; h = ; c = ; a = "
</PRE>
The other switches are standard for many applciations:
<DL>
<DT> -c</DT>
<DD> Print a (long) GNU GPL v3 copyright notice;</DD>
<DT> -a</DT>
<DD> Print a (short, 2 line) "about" string;</DD>
<DT> -h</DT>
<DD> Print some help regarding the proper ussage of this tool.</DD>
</DL>
The above string can be converted into an array of "array[switch]=default" 
in the the usual way:
<PRE>
 function s2a(str,a,sep,      tmp,n,i) {
	n= split(str,tmp,sep)
	for(i=1;i&lt;=n;i+=2)
		a[trim(tmp[i])]= trim(tmp[i+1])
 }
 function trim(s) { # trim leading/trailing blanks
	sub(/^[ \t]*/,"",s)
	sub(/[ \t]*$/,"",s)
	return s
 } 
</PRE>
<P>
For example, this code builds a array with keys "P,h,c,a" and values
"world,,," (respectively):
<PRE>
s2a("P = world; h = ; c = ; a = ",options, "[=;]")
</PRE>
<a name=5></a><H2> The Options function</H2>
<P>
To review: using <tt>s2a</tt> and strings like 
"P=world;h=;c=;a="
 we can quickly define switches and
their defaults. Also, if we start a switch with an upper case letter,
we can identify what switches take arguments.
That all said, we can now look at the code that changes a switch's default
value using command-line arguments:
<PRE>
function options(opt,input,n,               key,i,j,k,tmp) {
    for(i=1;i&lt;=n;i++)  {
        key = input[i]
        if (sub(/^[-]+/,"",key))  { # ........................ [1]
            if (key in opt)         # ........................ [2]
                opt[key] = (key ~ /^[A-Z]/) ? input[++i] : 1 # [3]
            else bad("-"key" unknown. Try -h for help.")     # [4]
        } else { i--; break }       # ........................ [5]
    }
    for(j=i+1;j&lt;=n;j++) tmp[j-i]=input[j] #................... [6]
    split("",input,"")                    #................... [7]
    for(k in tmp) input[k] = tmp[k]       #................... [8]
    return i-1                            #................... [9]
 }
 function warn(str) { print str >"/dev/stderr" }
 function bad(str)  { warn(str); exit}
</PRE>
<P>
The function updates the values in the <tt>opt</tt> array
then throws away the switches from the <tt>input</tt>. It then
returns the size of the remaining arguments (in Awk, this would be files
that the rest of the code would process).
<P>
This is not a complex function- explainaing it actually
takes nearly as many lines as the code itself:
<OL>
<LI>
 If we can remove a leading "<tt>-</tt>" for an argument, it is a flag.
<LI>
If we can find that flag in our list of pre-defined options, it is a legal flag.
<LI>
If the flag starts with an upper case letter, we assume that there is
an argument to read at the current input position, plus one. Else,
we just set that flag to "1".
<LI>
 If the next thing on the command line is not a flag, we have read one
item too many. If so, we:
<UL>
<LI> reset the pointer into the list of options back one value.</LI>
<LI> stop reading flags.</LI>
</UL>
<LI>
Now we have to remove the flags we read from the command-line array.
To do that, we first copy everything after the flags we read
 to  a temporary variable.
<LI> Then we reset the command-line array to the empty array.</LI>
<LI> Then we copy the contents of the temporary into the revised array.</LI>
<LI> Finally, we return the size of the newly created array.</LI>
</OL>
<P>
A standard usage of the function is:
is
<PRE>
s2a("a=;c=;h=;" str,opt,"[=;]")
ARGC = options(opt,ARGV,ARGC)
</PRE>
<P>
Note that
the idiom <tt>a=;c=;h=; str</tt>
combines a user supplied <tt>str</tt>
of switches with some standard switches.
<a name=6></a><H2> Example</H2>
<P>
 Shown below is a simple "hello world" application that accepts an optional "-P who" argument. By default,
"who=world" but for a more personnel greeting such as "hello ken", this code can be called with "-P ken".

<P>
This code also supports standard options such as  showing a
copyright notice, printing an about string, and showing help text.
This help text is defined in
the <em>usageHello</em> function:
<PRE>
 function usageHello() {
        about()        
        prints("Usage: hello [-P] ", " ",
        " -P string    who we shall greet. P='"opt("P")"'.",
        " -a           Show about notice (short).",
        " -c           Show copyright notice (long).",
        " -h           Help." )
 } 
</PRE>
<P>
The help text is printed if the <em>ok2go</em> function fails. Otherwise, the main function
is called:
<PRE>
                           #Key   = Default:
 BEGIN {    if (ok2go(Opt, "What  =  hello v0.1  ;"\
                           "When  =  2009        ;"\
                           "Who   =  Tim Menzies ;"\
                           "Why   =  hello world ;"\
                           "P     =  world        "))  
                { mainHello() }
             else usageHello()
 }
 function mainHello() {
        print "hello " opt("P")
 }   
</PRE>
<P> Note the call to <em>opt("P")</em>: this returns the value of the <em>-P</em> option- which defaults to "world". This function</P>
<P>
The <em>ok2go</em> function needs two argueents:
<OL>
<LI> An array that stores key=value pairs. In the above example, this array is "Opt".</LI>
<LI>
A string to initialize default keys. Each "key=default" pair is seperated by a semi-colon. If an option XXX To initialize a binary option,
use the idiom "p = ;".
</OL>
<a name=7></a><H2> Code</H2>

 function copyleft() {
	about()
    print ""
    print "This program is free software; you can redistribute it and/or"
    print "modify it under the terms of the GNU Lesser General Public"
    print "License as published by the Free Software Foundation; version 2.1."
    print ""
    print "This program is distributed in the hope that it will be useful"
    print "but WITHOUT ANY WARRANTY; without even the implied warranty of"
    print "MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU"
    print "Lesser General Public License for more details."
    print ""
    print "You should have received a copy of the GNU Lesser General Public"
    print "License along with this program; if not write to the Free Software"
    print "Foundation Inc. 51 Franklin St Fifth Floor Boston MA 02110-1301 USA."
 }
 function about() {
    print Opt["What"] " : " Opt["Why"];
    print "Copyright " Opt["When"] " " Opt["Who"] " (GPL version 3)";
 }

<P> Turn the string "key1,value1,key2,value2,.." into a array where a[key_i]=value_i</P>
 function prints(a0,b0,c0,d0,e0,f0,g0,h0,i0,j0,k0,l0,m0,n0,o0,p0,q0,r0,s0,t0,u0,v0,w0,x0,y0,z0,\
                 a1,b1,c1,d1,e1,f1,g1,h1,i1,j1,k1,l1,m1,n1,o1,p1,q1,r1,s1,t1,u1,v1,w1,x1,y1,z1,\
                 a2,b2,c2,d2,e2,f2,g2,h2,i2,j2,k2,l2,m2,n2,o2,p2,q2,r2,s2,t2,u2,v2,w2,x2,y2,z2,\
                 a3,b3,c3,d3,e3,f3,g3,h3,i3,j3,k3,l3,m3,n3,o3,p3,q3,r3,s3,t3,u3,v3,w3,x3,y3,z3,\
                 a4,b4,c4,d4,e4,f4,g4,h4,i4,j4,k4,l4,m4,n4,o4,p4,q4,r4,s4,t4,u4,v4,w4,x4,y4,z4,\
                 a5,b5,c5,d5,e5,f5,g5,h5,i5,j5,k5,l5,m5,n5,o5,p5,q5,r5,s5,t5,u5,v5,w5,x5,y5,z5,\
                 a6,b6,c6,d6,e6,f6,g6,h6,i6,j6,k6,l6,m6,n6,o6,p6,q6,r6,s6,t6,u6,v6,w6,x6,y6,z6,\
                 a7,b7,c7,d7,e7,f7,g7,h7,i7,j7,k7,l7,m7,n7,o7,p7,q7,r7,s7,t7,u7,v7,w7,x7,y7,z7,\
                 a8,b8,c8,d8,e8,f8,g8,h8,i8,j8,k8,l8,m8,n8,o8,p8,q8,r8,s8,t8,u8,v8,w8,x8,y8,z8,\
                 a9,b9,c9,d9,e9,f9,g9,h9,i9,j9,k9,l9,m9,n9,o9,p9,q9,r9,s9,t9,u9,v9) {
   prints26(a0,b0,c0,d0,e0,f0,g0,h0,i0,j0,k0,l0,m0,n0,o0,p0,q0,r0,s0,t0,u0,v0,w0,x0,y0,z0);
   prints26(a1,b1,c1,d1,e1,f1,g1,h1,i1,j1,k1,l1,m1,n1,o1,p1,q1,r1,s1,t1,u1,v1,w1,x1,y1,z1);
   prints26(a2,b2,c2,d2,e2,f2,g2,h2,i2,j2,k2,l2,m2,n2,o2,p2,q2,r2,s2,t2,u2,v2,w2,x2,y2,z2);
   prints26(a3,b3,c3,d3,e3,f3,g3,h3,i3,j3,k3,l3,m3,n3,o3,p3,q3,r3,s3,t3,u3,v3,w3,x3,y3,z3);
   prints26(a4,b4,c4,d4,e4,f4,g4,h4,i4,j4,k4,l4,m4,n4,o4,p4,q4,r4,s4,t4,u4,v4,w4,x4,y4,z4);
   prints26(a5,b5,c5,d5,e5,f5,g5,h5,i5,j5,k5,l5,m5,n5,o5,p5,q5,r5,s5,t5,u5,v5,w5,x5,y5,z5);
   prints26(a6,b6,c6,d6,e6,f6,g6,h6,i6,j6,k6,l6,m6,n6,o6,p6,q6,r6,s6,t6,u6,v6,w6,x6,y6,z6);
   prints26(a7,b7,c7,d7,e7,f7,g7,h7,i7,j7,k7,l7,m7,n7,o7,p7,q7,r7,s7,t7,u7,v7,w7,x7,y7,z7);
   prints26(a8,b8,c8,d8,e8,f8,g8,h8,i8,j8,k8,l8,m8,n8,o8,p8,q8,r8,s8,t8,u8,v8,w8,x8,y8,z8);
   prints26(a9,b9,c9,d9,e9,f9,g9,h9,i9,j9,k9,l9,m9,n9,o9,p9,q9,r9,s9,t9,u9);
 }
 function prints26(a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,x,y,z) {
   if (a) {print prints1(a) } else {return 1};
   if (b) {print prints1(b) } else {return 1};
   if (c) {print prints1(c) } else {return 1};
   if (d) {print prints1(d) } else {return 1};
   if (e) {print prints1(e) } else {return 1};
   if (f) {print prints1(f) } else {return 1};
   if (g) {print prints1(g) } else {return 1};
   if (h) {print prints1(h) } else {return 1};
   if (i) {print prints1(i) } else {return 1};
   if (j) {print prints1(j) } else {return 1};
   if (k) {print prints1(k) } else {return 1};
   if (l) {print prints1(l) } else {return 1};
   if (m) {print prints1(m) } else {return 1};
   if (n) {print prints1(n) } else {return 1};
   if (o) {print prints1(o) } else {return 1};
   if (p) {print prints1(p) } else {return 1};
   if (q) {print prints1(q) } else {return 1};
   if (r) {print prints1(r) } else {return 1};
   if (s) {print prints1(s) } else {return 1};
   if (t) {print prints1(t) } else {return 1};
   if (u) {print prints1(u) } else {return 1};
   if (v) {print prints1(v) } else {return 1};
   if (w) {print prints1(w) } else {return 1};
   if (x) {print prints1(x) } else {return 1};
   if (y) {print prints1(y) } else {return 1};
   if (z) {print prints1(z) } else {return 1};
 }
 function prints1(x) { # if starts with a " ", then indent this string
   return x ~ /^ /  ? "       "x : x; 
 }
 function opt(x) {
	return (x in Opt) ? Opt[x] : bad(Opt["What"] " option ["x"] unknown")
 }
 function ok2go(opt,str) { # returns 0 if bad options
	s2a("a=;c=;h=;" str,opt,"[=;]")
	ARGC = options(opt,ARGV,ARGC)
	if (opt("c")) { copyleft(); exit }
	if (opt("a")) { about();    exit }
	if (opt("h")) { return 0;        }
	return 1
 }
        

<a name=8></a><H2> Author</H2>
<P> Tim Menzies</P>
</div><!--- htmlbody --->
