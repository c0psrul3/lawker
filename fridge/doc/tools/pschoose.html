<h1><join> pschoose.awk</join></h1>
<h2>Contents</h2>
<div id="htmltoc">
<h2><font color=black>&bull;</font></a> <a href=#1>Synopsis</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#2>Download</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#3>Description</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#4>Details</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#5>Code</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#6>Author</a></h2>
</div><!--- htmltoc --->
<div id="htmlbody">
<a name=1></a><H2> Synopsis</H2>
<P> gawk -f pschoose</P>
<a name=2></a><H2> Download</H2>
<P> Download from <a href="http://lawker.googlecode.com/svn/fridge/lib/awk/pschoose.awk">LAWKER</a></P>
<a name=3></a><H2> Description</H2>
Pulls out a range of pages from postscript and just print those.
<a name=4></a><H2> Details</H2>
<P> <b>Pagerange</b> : list of pages from command line.</P>
<P> <b>Pages</b> : array with broken out list.</P>
<P>
 At end:
 	"(n in Pages)" is true if page n should be printed
<a name=5></a><H2> Code</H2>
<PRE>
Set up the list of paes to print.
function set_pagerange(        n, m, i, j, f, g)
{
	delete Pages

	n = split(Pagerange, f, ",")
	for (i = 1; i &lt;= n; i++) {
		if (index(f[i], "-") != 0) { # a range
			m = split(f[i], g, "-")
			if (m != 2 || g[1] >= g[2]) {
				printf("bad list of pages: %s\n",
					f[i]) > "/dev/stderr"
				exit 1
			}
			for (j = g[1]; j &lt;= g[2]; j++)
				Pages[j] = 1
		} else
			Pages[f[i]] = 1
	}
}

BEGIN {
	# constants
	TRUE = 1
	FALSE = 0

	if (ARGC != 3) {
		print "usage: pschoose range-spec file\n" > "/dev/stderr"
		exit 1
	}
	Pagerange = ARGV[1]
	delete ARGV[1]
	set_pagerange()
}

NR == 1, /^%%Page:/ {
	if (! /^%%Page/) {
		Prolog[++nprolog] = $0
		next
	}
}

/^%%Trailer/ || In_trailer {
	In_trailer = TRUE
	Epilog[++nepilog] = $0
	next
}

/^%%Page: /	{
	++Npage
	line = 0
}

 for all non-special lines
{
	# only save it if we will want to print it
	if (Npage in Pages)
		Page[Npage, ++line] = $0
}

END {
	# print the prologue
	for (i = 1; i in Prolog; i++)
		print Prolog[i]

	# print the actual body
	for (i = 1; i &lt;= Npage; i++) {
		if (i in Pages) {
			for (j = 1; (i, j) in Page; j++) {
				print Page[i, j]
			}
		}
	}

	# print the epilog
	for (i = 1; i in Epilog; i++)
		print Epilog[i]
}
</PRE>
<a name=6></a><H2> Author</H2>
<P> Arnold Robbins</P>
</div><!--- htmlbody --->
