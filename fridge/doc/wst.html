<h1><join> Waclaw Sierpinski's Triangle</join></h1>
<h2>Contents</h2>
<div id="htmltoc">
<h2><font color=black>&bull;</font></a> <a href=#1>Synopsis</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#2>Example</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#3>Code</a></h2>
<h2><font color=black>&bull;</font></a> <a href=#4>Author</a></h2>
</div><!--- htmltoc --->
<div id="htmlbody">
<a name=1></a><H2> Synopsis</H2>
<PRE>
gawk -f  wst.awk [-v X=anychar] iterations
</PRE>
<a name=2></a><H2> Example</H2>
<PRE>
 gawk -f wst.awk  -v X=* 2
               *
              * *
             *   *
            * * * *
           *       *
          * *     * *
         *   *   *   *
        * * * * * * * *
       *               *
      * *             * *
     *   *           *   *
    * * * *         * * * *
   *       *       *       *
  * *     * *     * *     * *
 *   *   *   *   *   *   *   *
* * * * * * * * * * * * * * * *
</PRE>
<a name=3></a><H2> Code</H2>
<PRE>
BEGIN {
    n = ARGV[1] + 0 # iterations
    if (n !~ /^[0-9]+$/) { exit(1) }
    if (n == 0) { width = 3 }
    row = split("X,X X,X   X,X X X X",A,",") # seed the array
    for (i=1; i&lt;=n; i++) { # build triangle
      width = length(A[row])
      for (j=1; j&lt;=row; j++) {
        str = A[j]
      # if (n &lt;= 9) { gsub(/[^ ]/,i,str) } # show structure
        A[j+row] = sprintf("%-*s %-*s",width,str,width,str)
      }
      row *= 2
    }
    for (j=1; j&lt;=row; j++) { # print triangle
      if (X != "") { gsub(/X/,substr(X,1,1),A[j]) }
      sub(/ +$/,"",A[j])
      printf("%*s%s\n",width-j+1,"",A[j])
    }
    exit(0)
}
</PRE>
<a name=4></a><H2> Author</H2>
<P>
Dan Nielsen
</div><!--- htmlbody --->
