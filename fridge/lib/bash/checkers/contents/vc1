#!/bin/sh

gawk -v my_color=$1 'function define_globals()
{
  FALSE = 0;
  TRUE = 1;
  EMPTY = 0;
  BLACK = 1;
  WHITE = 2;
  BOARD_SIZE = 8;
  DISPLAY = FALSE;
  EXIT_SUCCESS = 0;
  EXIT_FAILURE = 1;
}
BEGIN {
  define_globals();
  current_color = BLACK; ## Black goes first.
  if (my_color == "") {
    print_error("Error: no color given to me.");
    exit EXIT_FAILURE;
  } else if (my_color != WHITE && my_color != BLACK) {
    print_error("Error: invalid color: " color_to_string(my_color) \
                " (" my_color ")");
    exit EXIT_FAILURE;
  }
  their_color = flip_color(my_color);
  init(my_board);
  setup(my_board);
  srand();
  if (my_color == current_color) { ## Black goes first.
    DISPLAY = TRUE;
    move = 0;
    play(my_board, their_color, move);
    current_color = flip_color(current_color);
  } ## else continue expecting Black play
} ## end BEGIN
function play(board, color, move,    winning_color)
{
  if (!update(error, board, color, abs_val(move))) {
    print_error("Error: illegal move received: " error[0]);
    exit EXIT_SUCCESS;
  }
  if (DISPLAY && move > 0) {
    print_error(move " # move by " color_to_string(color));
    show(my_board);
  }
  if (move < 0) {
    if (winning_color = is_win(board)) {
       print_message("Winner! (color: " color_to_string(winning_color) ")");
       exit EXIT_SUCCESS;
    } else {
      print_error("Error: not an endgame move: " move);
      exit EXIT_SUCCESS;
    }
  } else if (winning_color = is_win(board)) {
    print_error("Error: move ended game: " move " (winner: " winner ")");
    exit EXIT_SUCCESS;
  }
  move = make_move(board, flip_color(color));
  print_move(move);
  if (DISPLAY && move > 0) {
    print_error(move " # move by " color_to_string(flip_color(color)));
    show(my_board);
  }
  if (move < 0) {
    exit EXIT_SUCCESS;
  }
  return TRUE;
}
current_color == their_color \
  && /^-?[1-9][1-9][1-9][1-9]$/ { ## ASSERT(BOARD_SIZE < 10)
  delete graph;
  play(my_board, their_color, $0);
}
! /^-?[1-9][1-9][1-9][1-9]$/ { ## ASSERT(BOARD_SIZE < 10)
  print_error("Error: bad input: " $0);
  exit EXIT_SUCCESS; ## Comment out for human-interactive play.
}
function setup(board,    i, j)
{
  for (i = 2; i <= BOARD_SIZE - 1; i++) {
    board[1, i] = BLACK;
    board[BOARD_SIZE, i] = BLACK;
    board[i, 1] = WHITE;
    board[i, BOARD_SIZE] = WHITE;
  }
}
function init(board,    i, j)
{
  for (i = 1; i <= BOARD_SIZE; i++) {
    for (j = 1; j <= 8; j++) {
      board[i, j] = EMPTY;
    }
  }
}
function show(board,    i, j, str)
{
  print_debug(color_to_string(BLACK) " = " BLACK ", " \
              color_to_string(WHITE) " = " WHITE);
  for (i = 1; i <= BOARD_SIZE; i++) {
    str = "";
    for (j = 1; j <= BOARD_SIZE; j++) {
      str = str board[i, j] " ";
    }
    print_debug(str);
  }
  print_debug("---------------");
}
function update(error, board, color, move,    m)
{
  if (move == 0) {
    return TRUE;
  }
  split(move, m, ""); ## ASSERT(BOARD_SIZE < 10)
  if (!is_legal_move(error, board, m[1], m[2], m[3], m[4], color)) {
    return FALSE;
  }
  board[m[3], m[4]] = board[m[1], m[2]];
  board[m[1], m[2]] = EMPTY;
  return TRUE;
}
function is_legal_move(error, board, row1, col1, row2, col2, color)
{
  move = row1 "" col1 "" row2 "" col2;
  if (!is_location(board, row1, col1)) {
      error[0] = "off board: " row1 col1;
      return FALSE;
  } else if (!is_location(board, row2, col2)) {
      error[0] = "off board: " row2 col2;
      return FALSE;
  } else if (board[row1, col1] == EMPTY) {
    error[0] = "no piece at: " row1 col1;
    return FALSE;
  } else if (board[row1, col1] != color) {
    error[0] = "Not your piece at: " row1 col1 " (expecting color: " \
               color_to_string(color) ")";
    return FALSE;
  } else if (!is_move_valid_length(board, row1, col1, row2, col2)) {
    error[0] = "wrong length move: " move;
    return FALSE;
  } else if (move_jumps_opponent(opp, board, row1, col1, row2, col2, color)) {
    error[0] = "Error: jumped opponent at: " opp[1] opp[2] " (move: " move ")";
    return FALSE;
  } else if (board[row2, col2] == EMPTY) {
    error[0] = "legal move: " move;
  } else {
    if (board[row1, col1] == board[row2, col2]) {
      error[0] = "move error: cannot land on your own piece";
      return FALSE;
    } else {
      error[0] = "capture at: " row2 col2 " (move: " move ")";
    }
  }
  return TRUE;
}
function move_jumps_opponent(pos, board, row1, col1,
			     row2, col2, color,    d_row, d_col)
{
  d_row = row2 - row1;
  d_row = d_row == 0 ? 0 : (d_row > 0 ? 1 : -1);
  d_col = col2 - col1;
  d_col = d_col == 0 ? 0 : (d_col > 0 ? 1 : -1);
  if (row1 + d_row == row2 && col1 + d_col == col2) {
    return FALSE;
  }
  return color_between(pos, board, row1 + d_row, col1 + d_col,
		       row2, col2, flip_color(color),
		       d_row, d_col);
}
function color_between(pos, board, row1, col1, row2, col2,
		       color, d_row, d_col)
{
  if (((row1, col1) in board && board[row1, col1] == color)) {
    pos[1] = row1;
    pos[2] = col1;
    return TRUE;
  } else if (((row2, col2) in board && board[row2, col2] == color)) {
    pos[1] = row2;
    pos[2] = col2;
    return TRUE;
  }
  if (row1 == row2 && col1 == col2) {
    return FALSE;
  }
  return color_between(pos, board, row1 + d_row, col1 + d_col,
                        row2, col2, color,
                        d_row, d_col);
}
function is_move_valid_length(board, row1, col1, row2, col2,    diag_length, n_pieces)
{
  if (is_horizontal_move(row1, col1, row2, col2)) {
    n_pieces = count_pieces_in_row(board, row1);
    return n_pieces == abs_val(col2 - col1);
  } else if (is_vertical_move(row1, col1, row2, col2)) {
    n_pieces = count_pieces_in_col(board, col1);
    return n_pieces == abs_val(row2 - row1);
  } else if (diag_length = is_diagonal(row1, col1, row2, col2)) {
    n_pieces = count_pieces_in_diagonal(board, row1, col1, row2, col2);
    return n_pieces == diag_length;
  } else {
    return FALSE;
  }
}
function count_pieces_in_diagonal(board, row1, col1,
				  row2, col2,     d_row, d_col)
{
  d_row = row2 - row1 > 0 ? 1 : -1;
  d_col = col2 - col1 > 0 ? 1 : -1;
  return count_pieces_in_line(board, row1, col1, d_row, d_col);
}
function count_pieces_in_line(board, row, col, d_row, d_col)
{
  return (((row, col) in board \
	   && board[row, col] == EMPTY) ? 0 : 1) \
    + count_pieces_from(board, row + d_row, col + d_col, d_row, d_col) \
    + count_pieces_from(board, row - d_row, col - d_col, -d_row, -d_col);
}
function count_pieces_from(board, row, col, d_row, d_col)
{
  if (!is_location(board, row, col)) {
    return 0; ## base case
  } ## else
  return count_pieces_from(board, row + d_row, col + d_col, d_row, d_col) \
    + (board[row, col] == EMPTY ? 0 : 1);
}
function is_location(board, row, col)
{
  return 1 <= row && row <= BOARD_SIZE \
           && 1 <= col && col <= BOARD_SIZE;
}
function count_pieces_in_row(board, row,    n, i)
{
  n = 0;
  for (i = 1; i <= BOARD_SIZE; i++) {
    n += (row, i) in board && board[row, i] == EMPTY ? 0 : 1;
  }
  return n;
}
function count_pieces_in_col(board, col,    n, i)
{
  n = 0;
  for (i = 1; i <= BOARD_SIZE; i++) {
    n += (i, col) in board && board[i, col] == EMPTY ? 0 : 1;
  }
  return n;
}
function is_diagonal(row1, col1, row2, col2,    d_row, d_col)
{
  d_row = row2 - row1;
  d_col = col2 - col1;
  return (abs_val(d_row) == abs_val(d_col) \
	  && d_row != 0) ? abs_val(d_row) : FALSE;
}
function is_horizontal_move(row1, col1, row2, col2)
{
  return row1 == row2;
}
function is_vertical_move(row1, col1, row2, col2)
{
  return col1 == col2;
}
function flip_color(color)
{
  return color == WHITE ? BLACK : WHITE;
}
function color_to_string(color)
{
  return color == BLACK ? "Black" : (color == WHITE ? "White" : "");
}
function is_win(board,    black_won, white_won)
{
  black_won = has_won(board, BLACK);
  white_won = has_won(board, WHITE);
  if (black_won) {
    if (white_won) {
      print_error("Error: Draw (tie)!");
      exit EXIT_FAILURE;
    }
    return BLACK;
  } else if (white_won) {
    return WHITE;
  } ## else
  return FALSE;
}
function has_won(board, color,    n_pieces, pieces, n_edges, edge)
{
  n_pieces = make_edge_graph(pieces, n_edges, edge, board, color);
  if (edges_continuous(pieces, n_edges, edge, n_pieces)) {
    return color; ## ASSERT(EMPTY == 0 && color != EMPTY)
  }
  delete n_edges;
  delete edge;
  delete pieces;
  return FALSE;
}
function edges_continuous(pieces, n_edges, edge, n_pieces,    colored,
			  i, j, piece)
{
  if (n_pieces <= 1) {
    return TRUE;
  } ## else
  piece = pieces[1];
  n_colored = color_connected_to(colored, n_edges, edge, piece);
  delete colored;
  return n_colored == n_pieces;
}
function color_connected_to(colored, n_edges, edge, piece,    i, n_colored)
{
  n_colored = 0;
  colored[piece] = 1;
  for (i = 1; i <= n_edges[piece]; i++) {
    if (!(edge[i, piece] in colored)) {
      n_colored \
	+= color_connected_to(colored, n_edges, edge, edge[i, piece]);
    }  
  }
  return n_colored + 1;
}
function make_edge_graph(pieces, n_edges, edge, board, color,    n_pieces, row, col)
{
  n_pieces = 0;
  for (row = BOARD_SIZE; row >= 1; row--) {
    for (col = BOARD_SIZE; col >= 1; col--) {
      if (!is_location(board, row, col)) {
        return TRUE;
      } else if (board[row, col] == color) {
        n_pieces++;
        pieces[n_pieces] = row SUBSEP col;
        if (is_location(board, row, col - 1) \
            && board[row, col - 1] == color) {
          add_edge(n_edges, edge, row, col, row, col - 1, color);
        }
        if (is_location(board, row - 1, col) \
            && board[row - 1, col] == color) {
          add_edge(n_edges, edge, row, col, row - 1, col, color);
        }
        if (is_location(board, row - 1, col - 1) \
            && board[row - 1, col - 1] == color) {
          add_edge(n_edges, edge, row, col, row - 1, col - 1, color);
        }
      } else if (board[row, col - 1] == color \
                 && board[row - 1, col] == color) {
        add_edge(n_edges, edge, row, col - 1, row - 1, col, color);
      }
    }
  }
  return n_pieces;
}
function add_edge(n_edges, edge, row1, col1, row2, col2, color)
{
  n_edges[row1, col1]++; 
  n_edges[row2, col2]++;
  edge[n_edges[row1, col1], row1, col1] = row2 SUBSEP col2;
  edge[n_edges[row2, col2], row2, col2] = row1 SUBSEP col1;
}
function abs_val(val)
{
  return val < 0 ? -val : val;
}
function print_move(move)
{
  print move;
  fflush();
}
function print_message(str)
{
  print_error("# " str);
}
function print_debug(str)
{
  print_error("#DEBUG " str);
}
function print_error(str)
{
  print str > "/dev/stderr";
}
function print_array(array,    i)
{
  print_debug("array(");
  for (i in array) {
    print_debug("\t[" i "] => " array[i]);
  }
  print_debug(")");
}
function make_move(board, color,    moves, n_moves, move)
{
  find_all_moves(moves, board, color);
  n_moves = moves[0];
  move = make_random_move(board, moves, color, n_moves)
  if (!update(error, board, color, move)) {
    print_error("Error: illegal move generated: " error[0]);
    exit EXIT_SUCCESS;
  }
  delete moves;
  return is_win(board) ? -move : move;
}
function make_random_move(board, moves, color, n_moves,    move_num)
{
  move_num = 1;
  return moves[move_num];
}
function find_all_moves(moves, board, color,    distances, row, col)
{
  moves[0] = 0;
  for (row = BOARD_SIZE; row >= 1; row--) {
    for (col = BOARD_SIZE; col >= 1; col--) {
      if ((row, col) in board \
          && board[row, col] == color) {
        all_distances_at(distances, board, row, col);
        all_legal_moves_at(moves, distances, board, row, col, color);
      }
    }
  }
}
function all_legal_moves_at(moves, distances, board, row, col,
			    color,    n_moves, direction, distance)
{
  for (direction in distances) {
    split(direction, delta, SUBSEP);
    d_row = delta[1];
    d_col = delta[2];
    distance = distances[direction];
    get_move_in_direction(moves, board, row, col, d_row, d_col,
			  distance, color);
    get_move_in_direction(moves, board, row, col, -d_row, -d_col,
			  distance, color);
  }
}
function get_move_in_direction(moves, board, row, col, d_row, d_col,
			       distance, color,    row2, col2, error, n_moves)
{
  row2 = row + (distance * d_row);
  col2 = col + (distance * d_col);
  if (is_legal_move(error, board, row, col, row2, col2, color)) {
    moves[0]++;
    n_moves = moves[0];
    moves[n_moves] = row "" col "" row2 "" col2;
  }
}
function all_distances_at(distances, board, row, col)
{
  n_diagonal_up = count_pieces_in_line(board, row, col, -1, 1);
  n_diagonal_down = count_pieces_in_line(board, row, col, 1, 1);
  n_horizontal = count_pieces_in_line(board, row, col, 1, 0);
  n_vertical = count_pieces_in_line(board, row, col, 0, 1);
  distances[0, 1] = n_vertical;
  distances[0, -1] = n_vertical;
  distances[-1, 1] = n_diagonal_up;
  distances[1, -1] = n_diagonal_up;
  distances[1, 0] = n_horizontal;
  distances[-1, 0] = n_horizontal;
  distances[1, 1] = n_diagonal_down;
  distances[-1, -1] = n_diagonal_down;
  return;
}'

